defmodule RetoElixir.Infrastructure.EntryPoint.RestController.SignUp.Infra.RouterSignUp do
  use Plug.Router
  alias RetoElixir.Utils.ContextData
  alias RetoElixir.Domain.UseCase.SignUp.SignupUseCase
  alias RetoElixir.Infrastructure.EntryPoint.RestController.ErrorHandler

  plug Plug.Parsers, parsers: [:json], pass: ["application/json"], json_decoder: Jason
  plug :match
  plug :dispatch

  defp build_context(conn) do
    message_id = get_req_header(conn, "message-id") |> List.first() || UUID.uuid4()
    x_request_id = get_req_header(conn, "x-request-id") |> List.first() || UUID.uuid4()

    %ContextData{
      message_id: message_id,
      x_request_id: x_request_id
    }
  end

  post "/api/signup" do
    ctx = build_context(conn)

    case SignupUseCase.execute(conn.body_params, ctx) do
      {:ok, :created} ->
        conn
        |> put_resp_content_type("application/json")
        |> send_resp(201, "")
      {:error, reason} ->
        ErrorHandler.handle_error(conn, reason, ctx)
    end
  end

  match _ do
    ctx = build_context(conn)
    ErrorHandler.handle_error(conn, :malformed_request, ctx)
  end

end
