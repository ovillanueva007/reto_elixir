defmodule RetoElixir.Infrastructure.EntryPoint.RestController.ErrorHandler do
  import Plug.Conn
  alias RetoElixir.Utils.ContextData

  @error_codes %{
    invalid_email_format: {400, "Invalid email"},
    weak_password: {400, "Password invalid"},
    malformed_request: {400, "Malformed request"},
    invalid_credentials: {401, "Invalid credentials"},
    email_already_exists: {409, "Email already registered"},
    unexpected_error: {500, "Unexpected error"}
  }

  def handle_error(conn, error_atom, %ContextData{} = ctx) do
    {status, message} = Map.get(@error_codes, error_atom, {500, "Unknown error"})
    code_str = error_atom |> Atom.to_string() |> String.upcase()

    body = %{
      error: %{
        code: code_str,
        message: message,
        details: %{},
        correlation: %{
          message_id: ctx.message_id,
          x_request_id: ctx.x_request_id
        }
      }
    }

    conn
    |> put_resp_content_type("application/json")
    |> send_resp(status, Jason.encode!(body))
  end
end
